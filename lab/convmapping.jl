struct C{N} end
(::Type{C{N}})((t, b))  where N = (t-(N-1)÷2, b+N÷2)
down(::Type{<:C}, h) = h
str(::Type{C{N}}) where N = "C$N"

struct P{N} end
(::Type{P{N}})((t, b)) where N = (N*t, N*b+N-1)
down(::Type{P{N}}, h) where N = h÷N
str(::Type{P{N}}) where N = "P$N"

function A(a, ht)
    println()
    join(str.(a), "-") |> println
    m(t) = foldr(∘, a)((t, t))
    finalht = foldr(∘, [h->down(l, h) for l in a])(ht)
    ifinal = 0:(finalht-1)
    maps = m.(ifinal)
    mapwd = maps[1][2]-maps[1][1]+1
    olap = maps[1][2]-maps[2][1]+1
    olappc = Int(round(olap/mapwd*100))
    println("$ht ⇒ $finalht maps ht: $mapwd overlap: $olap ($olappc%)")
    println(maps)
    println()
end

A(repeat([C{3}, P{2}], 5), 96)
# C3-P2 C3-P2 C3-P2 C3-P2 C3-P2
# 96 ⇒ 3 maps ht: 94 overlap: 62 (66%)
# [(-31, 62), (1, 94), (33, 126)]

A(repeat([C{3}, P{2}], 4), 96)
# C3-P2 C3-P2 C3-P2 C3-P2
# 96 ⇒ 6 maps ht: 46 overlap: 30 (65%)
# [(-15, 30), (1, 46), (17, 62), (33, 78), (49, 94), (65, 110)]

A(repeat([C{3}, P{2}], 3), 96)
# C3-P2 C3-P2 C3-P2
# 96 ⇒ 12 maps ht: 22 overlap: 14 (64%)
# [(-7, 14), (1, 22), (9, 30), (17, 38), (25, 46), (33, 54), (41, 62), (49, 70), (57, 78), (65, 86), (73, 94), (81, 102)]

A(repeat([C{3}, C{3}, P{2}], 3), 96)
# C3-C3-P2 C3-C3-P2 C3-C3-P2
# 96 ⇒ 12 maps ht: 36 overlap: 28 (78%)
# [(-14, 21), (-6, 29), (2, 37), (10, 45), (18, 53), (26, 61), (34, 69), (42, 77), (50, 85), (58, 93), (66, 101), (74, 109)]

A([C{3}, P{3}, C{3}, P{2}, C{3}, P{2}], 96)
# C3-P3 C3-P2 C3-P2
# 96 ⇒ 8 maps ht: 32 overlap: 20 (62%)
# [(-10, 21), (2, 33), (14, 45), (26, 57), (38, 69), (50, 81), (62, 93), (74, 105)]

A([C{3}, P{2}, C{3}, P{3}, C{3}, P{2}], 96)
# C3-P2 C3-P3 C3-P2
# 96 ⇒ 8 maps ht: 30 overlap: 18 (60%)
# [(-9, 20), (3, 32), (15, 44), (27, 56), (39, 68), (51, 80), (63, 92), (75, 104)]

A([C{3}, P{2}, C{3}, P{2}, C{3}, P{3}], 96)                                         # Like
# C3-P2 C3-P2 C3-P3
# 96 ⇒ 8 maps ht: 26 overlap: 14 (54%)
# [(-7, 18), (5, 30), (17, 42), (29, 54), (41, 66), (53, 78), (65, 90), (77, 102)]

A([C{3}, C{3}, P{3}, C{3}, C{3}, P{2}, C{3}, C{3}, P{2}], 96)
# C3-C3-P3 C3-C3-P2 C3-C3-P2
# 96 ⇒ 8 maps ht: 52 overlap: 40 (77%)
# [(-20, 31), (-8, 43), (4, 55), (16, 67), (28, 79), (40, 91), (52, 103), (64, 115)]

A([C{3}, C{3}, P{2}, C{3}, C{3}, P{3}, C{3}, C{3}, P{2}], 96)
# C3-C3-P2 C3-C3-P3 C3-C3-P2
# 96 ⇒ 8 maps ht: 48 overlap: 36 (75%)
# [(-18, 29), (-6, 41), (6, 53), (18, 65), (30, 77), (42, 89), (54, 101), (66, 113)]

A([C{3}, C{3}, P{2}, C{3}, C{3}, P{2}, C{3}, C{3}, P{3}], 96)
# C3-C3-P2 C3-C3-P2 C3-C3-P3
# 96 ⇒ 8 maps ht: 40 overlap: 28 (70%)
# [(-14, 25), (-2, 37), (10, 49), (22, 61), (34, 73), (46, 85), (58, 97), (70, 109)]
